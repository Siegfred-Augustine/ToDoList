plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'org.todo'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '22'
targetCompatibility = '22'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.todo.todolist'
    mainClass = 'org.todo.todolist.Main'
}

javafx {
    version = '22.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.12.1'
    implementation 'net.java.dev.jna:jna-platform:5.12.1'
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation 'org.openjfx:javafx-controls:22.0.1'
    implementation 'org.openjfx:javafx-fxml:22.0.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform()
}

// Task to build tracker.jar
tasks.register('trackerJar', Jar) {
    archiveBaseName = 'tracker'
    archiveVersion = ''
    archiveClassifier = ''
    destinationDirectory = file("$buildDir/libs")
    
    manifest {
        attributes(
            'Main-Class': 'org.todo.todolist.BackgroundTracker',
            'Multi-Release': 'true'
        )
    }
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from(sourceSets.main.output)
}

// Make trackerJar run as part of the build task
tasks.build.dependsOn trackerJar

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addOptions('--add-modules', 'javafx.controls,javafx.fxml')
    
    launcher {
        name = 'FocusFlow'
    }
    
    jpackage {
        appVersion = '1.0'
        imageName = 'FocusFlow'
        installerName = 'FocusFlowSetup'
        installerType = 'exe'
        description = 'FocusFlow Task Manager'
        jvmArgs = ['-Xmx512m']
    }
}